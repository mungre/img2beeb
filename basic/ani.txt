MODE7:PRINT:*FX21,0
HIMEM=HIMEM-&200
PROCmenu:CLS
MODE2
HIMEM=HIMEM-&100
VDU 23,1,0;0;0;0;
FORC%=0TO15:VDU19,C%,0;0;:NEXT
OSCLI"LOAD "+P$+" "+STR$~(HIMEM)
PROCasm
CALLani
CLS:RUN
DEFPROCmenu
*DIR P
M%=HIMEM
B%=M%+&100
?M%=0:M%!1=B%:M%!5=256:M%!9=0
A%=8:X%=M%:Y%=M%DIV256
CALL&FFD1
E%=M%!1
P%=B%
A%=0
REPEATIF P%<>E% THEN VDUA%+48,58,32:PROCread:PRINTP$:UNTIL0 ELSEUNTIL1
REPEATK%=GET-48:IFK%<0ORK%>=A%UNTIL0 ELSEUNTIL1
P%=B%
A%=-1
REPEATIF A%<>K% THEN PROCread:UNTIL0 ELSEUNTIL1
ENDPROC
DEFPROCread
D%=M%+128
FORS%=1TO?P%
?D%=P%?S%
D%=D%+1
NEXT
?D%=13
P$=$(M%+128)
P%=P%+?P%+1
A%=A%+1
ENDPROC
DEFPROCasm
H%=HIMEM
T%=H%?&10
S%=H%?&11
V%=H%?&12
M%=H%+&13
DIM Z% 100
FOR W%=0TO2STEP2
P%=Z%
[OPTW%
.ani
LDY #(S%-1) AND 255
BMI mloop
.sloop
LDX M%,Y
JSR pal
DEY
BPL sloop
.mloop
LDA #T%:STA &70
LDA #0:STA &71
.floop
LDY #S%
.cloop
JSR next
JSR pal
INY
CPY #S%+V%
BNE cloop
JSR next
LDA#&81:LDY #&00:JSR&FFF4
CPY#&FF
BNE quit
DEC &70
BNE floop
BEQ mloop
.quit
RTS
.pal
LDA #&13:JSR&FFEE
TYA:JSR &FFEE
TXA:JSR &FFEE
LDA #&00:JSR &FFEE:JSR &FFEE:JMP &FFEE
.next
LDX &71
INC &71
LDA M%+S%,X
TAX
RTS
]
NEXT
ENDPROC
